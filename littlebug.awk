#!/usr/bin/awk -f

#大きな範囲のタグなど、そのタグ自体で単一行とするようなものの追加は、その場で直接print出力する(行追加される)。
#それ以外の小さなタグは行内挿入/部分置換によってline変数内で行い、
#MAINセクションの最後もしくはnextコマンドの直前でline変数をprint出力する(変換後の行が出力される)。

BEGIN {
  state_p = "none"     # none, discript, bracket
  state_section = "none"     # none, section
  in_quote = 0       # 1 = 「」の内部を処理中
}

{
  #############################################################################
  ## 初期処理、行全体的な処理                                                ##
  ##                                                                         ##
  ##                                                                         ##
  #############################################################################
  #空行は出力しない
  if ($0 ~ /^$/) { next; }

  #置換後・出力用の文言を創出しつつ
  #判定自体は元の行の形を用いるため、$0とlineを別に設ける
  line = $0

  #############################################################################
  ## 段落系処理                                                              ##
  ##                                                                         ##
  ##                                                                         ##
  #############################################################################

  ###########################################################
  # section(中段落)  
  ###########################################################
  #行頭に§が登場する場合、セクション名タグを付与し、セクションタグを開始する。
  if ($0 ~ /^§+/) {

    #初回の場合、pタググループは開始されていないので閉じない
    #初回以外では、セクションの境界でpタググループを必ず閉じる
    if (state_p != "none") {
        print "  </div>"
        state_p = "none"
    }
    #初回の場合、セクションを開始する(セクション状態を開始する)
    #初回以外では、セクションを閉じてから開き直す
    if (state_section != "none") {
      print "</section>"
    }
    print "<section class=\"section\">"
    state_section = "section"

    # §の行自体にもルビなどの置換を適用したい場合はここに記述
    line = gensub(/｜([^《]+)《([^》]+)》/, "<ruby>\\1<rt>\\2</rt></ruby>", "g", line);
    line = gensub(/《《([^》]+)》》/, "<emphasis>\\1</emphasis>", "g", line);
    
    line = gensub(/(§+.*)/, "  <h2 class=\"section_name\">\\1</h2>", "g", line);

    print line
    next
  }

  ###########################################################
  # div(中段落)  
  ###########################################################
  # 現在の行に開き括弧類が含まれるか（開始判定）
  # 含まれていれば、以降をクオート状態とし、次に行末閉じ括弧類が現れるまでクオート中を維持する
  # if ($0 ~ /^「/) { in_quote = 1}
  if ($0 ~ /^[「『（〝＞―]/) { in_quote = 1}

  # 判定：現在、クオート状態が継続中であるか、もしくは行頭開き括弧類か
  # もしそうであれば、pタグのクラスをbracketにする。
  # これに該当しない場合はクラスはdiscriptとなる
  # if ( in_quote == 1 || $0 ~ /^「/) {
  if ( in_quote == 1 || $0 ~ /^[「『（〝＞―]/) {
    current_type = "bracket-group"
  } else {
    current_type = "discript-group"
  }

  # 状態が変わった（または最初の行）場合のタグ挿入
  if (state_p != current_type) {
    if (state_p != "none") { print "  </div>" }
    print "  <div class=\"" current_type "\">"
    state_p = current_type
  }

  ###########################################################
  # p(小段落)
  ###########################################################
  line = gensub(/^([「『（〝＞―])([^」』）〟＜―]+)([」』）〟＜―])$/, "    <p class=\"bracket\" data-header=\"\\1\" data-footer=\"\\3\">\\2</p><!--bracket-->", "g", line);
  line = gensub(/^([「『（〝＞―])([^」』）〟＜―]+)$/, "    <p class=\"bracket\" data-header=\"\\1\" data-footer=\"-\">\\2</p><!--bracket-->", "g", line);
  line = gensub(/^[^「『（〝＞―](.+)[」』）〟＜―]$/, "    <p class=\"bracket\" data-header=\"-\" data-footer=\"」\">\\1</p><!--bracket-->", "g", line);
  line = gensub(/^　(.+)$/, "    <p class=\"discript\" data-header=\"　\" data-footer=\"-\">\\1</p><!--descript-->", "g", line);


  #############################################################################
  ## 通常の変換                                                              ##
  ##                                                                         ##
  ##                                                                         ##
  #############################################################################
  ###########################################################
  # ルビ
  ###########################################################
  line = gensub(/｜([^《]+)《([^》]+)》/, "<ruby>\\1<rt>\\2</rt></ruby>", "g", line);

  ###########################################################
  # 傍点
  ###########################################################
  line = gensub(/《《([^》]+)》》/, "<emphasis>\\1</emphasis>", "g", line);

  # 行の出力
  print line


  #############################################################################
  ## 行末処理                                                                ##
  ##                                                                         ##
  ##                                                                         ##
  #############################################################################
  # 行末 が」 かどうか（終了判定）
  # 行末が」であれば、現在継続中のクオート状態を解除する。
  if ($0 ~ /[」』）〟＜―]$/) { in_quote = 0 }
}

END {
  #一度もpタグが登場していない場合、閉じる必要がない(ほぼあり得ないが)
  if (state_p != "none") { print "  </div>" }

  #一度もsectionタグが登場していない場合、閉じる必要がない(割とあり得る)
  if (state_section != "none") { print "</section>" }
}
